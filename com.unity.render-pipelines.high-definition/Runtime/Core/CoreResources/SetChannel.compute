#pragma only_renderers d3d11 ps4 xboxone vulkan metal switch

#pragma kernel SetRT Red    FromTex KerName=SetRT
#pragma kernel SetRV Red    FromVal KerName=SetRV
#pragma kernel SetGT Green  FromTex KerName=SetGT
#pragma kernel SetGV Green  FromVal KerName=SetGV
#pragma kernel SetBT Blue   FromTex KerName=SetBT
#pragma kernel SetBV Blue   FromVal KerName=SetBV
#pragma kernel SetAT Alpha  FromTex KerName=SetAT
#pragma kernel SetAV Alpha  FromVal KerName=SetAV

#include "Packages/com.unity.render-pipelines.core/ShaderLibrary/Common.hlsl"
#include "Packages/com.unity.render-pipelines.core/ShaderLibrary/Macros.hlsl"
#include "Packages/com.unity.render-pipelines.core/ShaderLibrary/Sampling/Sampling.hlsl"
#include "Packages/com.unity.render-pipelines.core/ShaderLibrary/Packing.hlsl"
#include "Packages/com.unity.render-pipelines.high-definition/Runtime/ShaderLibrary/ShaderVariables.hlsl"

Texture2D<float4>   _InputTex;
          float     _InputVal;
RWTexture2D<float4> _Output;

uint4               _Sizes; // xy: InputSize; zw: OutputSize

#define _InputSize  _Sizes.xy
#define _OutputSize _Sizes.zw

SAMPLER(sampler_LinearClamp);

[numthreads(8, 8, 1)]
void KerName(uint3 id : SV_DispatchThreadID)
{
    if (all(id.xy < _OutputSize))
    {
        float4 v = _Output[id.xy];

#ifdef Red
#   ifdef FromTex
        _Output[id.xy] = float4(_InputTex[uint2(0, 0)].w, v.yzw);
#   elif defined(FromVal)
        _Output[id.xy] = float4(_InputVal, v.yzw);
#   else
#       error SetChannelCompute.compute: FromTex/Val not defined
#   endif
#elif defined(Green)
#   ifdef FromTex
        _Output[id.xy] = float4(v.x, _InputTex[uint2(0, 0)].w, v.zw);
#   elif defined(FromVal)
        _Output[id.xy] = float4(v.x, _InputVal, v.zw);
#   else
#       error SetChannelCompute.compute: FromTex/Val not defined
#   endif
#elif defined(Blue)
#   ifdef FromTex
        _Output[id.xy] = float4(v.xy, _InputTex[uint2(0, 0)].w, v.w);
#   elif defined(FromVal)
        _Output[id.xy] = float4(v.xy, _InputVal, v.w);
#   else
#       error SetChannelCompute.compute: FromTex/Val not defined
#   endif
#elif defined(Alpha)
#   ifdef FromTex
        _Output[id.xy] = float4(v.xyz, _InputTex[uint2(0, 0)].w);
#   elif defined(FromVal)
        _Output[id.xy] = float4(v.xyz, _InputVal);
#   else
#       error SetChannelCompute.compute: FromTex/Val not defined
#   endif
#else
#   error SetChannelCompute.compute: Channel not defined
#endif
    }
}
